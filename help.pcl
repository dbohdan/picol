#! /usr/bin/env picolsh
# -- help.pcl -- extract usage hints from picol.h
# Usage notes are marked with the ARITY2 macro in picol.h.  This script
# extracts them and adds commands that don't have a note from
# [info commands].  It shows string manipulation and the use of arrays and
# regular expressions in Picol.

set file [if {$argc == 0} { lindex picol.h } else { lindex $argv 0 }]
set f [open $file]
puts "Picol [info pa] has [llength [info commands]] commands"

while {[gets $f line] >= 0} {
    if {[string first ARITY2 $line] == -1} continue
    if {[string match #define* $line]} continue

    set first [+ 1 [string first \" $line]]
    set last  [expr {[string last \" $line] - 1}]
    set line  [string range $line $first $last]

    set cmd [lindex $line 0]
    if {[regexp ^\s*$ $cmd]} continue
    lappend cmds $cmd
    set info($cmd) [string trim [string range $line [string length $cmd] end]]
}
close $f

puts "[llength $cmds] annotations\n"
foreach cmd [info commands] {
    if {[string match _* $cmd]} continue
    if {[lsearch -exact $cmds $cmd] < 0} {
        lappend cmds $cmd
        set info($cmd) "\[no annotation\]"
    }
}

foreach cmd [lsort -unique $cmds] { puts "$cmd [set info($cmd)]" }
